apply plugin: 'com.android.application'
//打包时间戳
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.wonium.aclj"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 5
        versionName "0.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false  //这个属性 网上说是  每次都会检查lint 错误， 所以设置成false 忽略检查
    }
    signingConfigs{
        release{
            keyAlias 'release'  //别名
            keyPassword 'wonium1993' // 别名密码
            storeFile file('keystore/release.jks')//签名文件路径
            storePassword 'wonium1993'//签名密码
        }
        debug{
            keyAlias 'debug'
            keyPassword 'wonium1993'
            storeFile file('keystore/debug.jks')
            storePassword 'wonium1993'
        }
    }
    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true //代码混淆
            zipAlignEnabled true //zipalign 优化
            // 移除无用的resource文件
            shrinkResources false
            //加载默认混淆配置文件 progudard-android.txt在sdk目录里面，不用管，proguard.cfg是我们自己配<span></span>的混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //打包签名
            signingConfig signingConfigs.release
            applicationVariants.all {
                variant -> variant.outputs.all {
                    output -> outputFileName=" ${variant.applicationId}- ${ buildType.name}- ${defaultConfig.versionName}-${ defaultConfig.versionCode }-${releaseTime() }.apk"
                }
            }
        }
        debug {
            // 这个是控制显示LOG true 显示 false 不显示
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug" //测试包版本名后缀 这个在安装的时候，会看到
            minifyEnabled false //代码混淆 false 不混淆，true 混淆
            zipAlignEnabled true // zipalign 优化 这个也要单独领出来说一下
            shrinkResources false //移除无用的resource文件 signingConfig signingConfigs.debug //签名调用
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }

    flavorDimensions "default"
    // 多渠道批量打包 渠道名
    productFlavors {
        xiaomi {
            dimension "default"
        //  manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
        _360 {
            dimension "default"
        //  manifestPlaceholders = [UMENG_CHANNEL_VALUE: "_360"]
        }
        baidu {
            dimension "default"
        //  manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu"]
        }
        wandoujia {
            dimension "default"
        //  manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    //资源设置
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    implementation 'com.android.support:design:28.0.0'
    implementation project(path: ':cicada-android')
}
